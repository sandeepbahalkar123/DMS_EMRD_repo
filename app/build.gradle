apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            keyAlias 'scorg'
            keyPassword 'scorg123'
            storeFile file('scorg_key.jks')
            storePassword 'scorg123'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId "com.scorg.dms"
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
        signingConfig signingConfigs.config
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res',
                            'src/main/res/layouts',
                            'src/main/res/layouts/doctor',
                            'src/main/res/layouts/patient/patient_connect',
                            'src/main/res/layouts/patient/patient_appointment',
                            'src/main/res/layouts/dashboard',
                            'src/main/res/layouts/common',
                            'src/main/res/layouts/waiting_list',
                            'src/main/res/layout-sw720dp',
                            'src/main/res/layout-sw600dp']
        }
    }

    repositories {
        maven {
            url "https://jitpack.io"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':library')
    implementation project(':android-pdf-viewer')
    implementation(project(':bottomsheetpickers')) {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    implementation "com.android.support:appcompat-v7:${project.ANDROID_SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${project.ANDROID_SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:cardview-v7:${project.ANDROID_SUPPORT_LIBRARY_VERSION}"
    implementation('com.github.hotchemi:permissionsdispatcher:2.4.0') {
    // if you don't use android.app.Fragment you can exclude support for them
        exclude module: "support-v13"
    }
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.mcxiaoke.volley:library:1.0.19'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'com.github.johnkil.print:print:1.3.1'
    implementation 'com.github.bumptech.glide:glide:4.7.1'

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

//    implementation 'info.hoang8f:android-segmented:1.0.6'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "${project.ANDROID_SUPPORT_LIBRARY_VERSION}"
            }
        }
    }
}
